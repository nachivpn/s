{-# OPTIONS --safe --without-K #-}

open import Semantics.Category.Base
open import Semantics.Category.Cartesian
open import Semantics.Category.CartesianClosed
open import Semantics.Category.EndoFunctor.Base
open import Semantics.Category.EndoFunctor.Multiplicative
open import Semantics.Category.EndoFunctor.Strong.Base
open import Semantics.Category.EndoFunctor.Strong.Multiplicative

open import JFC.Term.Base
open import JFC.Term.Properties
open import JFC.Term.Conversion

open import Relation.Binary.PropositionalEquality
  using    (_â‰¡_ ; refl ; cong ; congâ‚‚ ; module â‰¡-Reasoning ; subst ; substâ‚‚)
  renaming (sym to â‰¡-sym ; trans to â‰¡-trans ; isEquivalence to â‰¡-equiv)
import Relation.Binary.Reasoning.Setoid as EqReasoning

module JFC.Term.Model where

open import Level using (0â„“)

infix 19 _âŠ¢_

_âŠ¢_ : Ty â†’ Ty â†’ Set
a âŠ¢ b = Tm [ a ] b

[_]â‚› : Tm Î“ a â†’ Sub Î“ [ a ]
[ t ]â‚› = [] `, t

[-]â‚›-pres-â‰ˆ : t â‰ˆ t' â†’ [ t ]â‚› â‰ˆâ‚› [ t' ]â‚›
[-]â‚›-pres-â‰ˆ tâ‰ˆt' = [] `, tâ‰ˆt'

v0â‚œ[_] : (a : Ty) â†’ Tm (Î“ `, a) a
v0â‚œ[ _ ] = var v0

v0â‚œ : Tm (Î“ `, a) a
v0â‚œ = v0â‚œ[ _ ]

v1â‚œ : Tm ((Î“ `, a) `, b) a
v1â‚œ = var v1

âŠ¢-refl[_] = v0â‚œ[_]

id[_]  = v0â‚œ[_]

id : a âŠ¢ a
id = v0â‚œ

âŠ¢-trans : a âŠ¢ b â†’ b âŠ¢ c â†’ a âŠ¢ c
âŠ¢-trans t u = substTm [ t ]â‚› u


infix 21 _âŸ¨_âŸ©

_âŸ¨_âŸ© :  b âŠ¢ c â†’ a âŠ¢ b â†’ a âŠ¢ c
u âŸ¨ t âŸ© = âŠ¢-trans t u

âŸ¨-âŸ©-pres-â‰ˆ : t â‰ˆ t' â†’ u â‰ˆ u' â†’ t âŸ¨ u âŸ© â‰ˆ t' âŸ¨ u' âŸ©
âŸ¨-âŸ©-pres-â‰ˆ  tâ‰ˆt' uâ‰ˆu' = substTm-pres-â‰ˆ ([-]â‚›-pres-â‰ˆ uâ‰ˆu') tâ‰ˆt'

âŸ¨-âŸ©-unit-right : (a : Ty) {b : Ty} (t : a âŠ¢ b) â†’ t âŸ¨ id âŸ© â‰ˆ t
âŸ¨-âŸ©-unit-right _ t = â‰¡-to-â‰ˆ (substTm-pres-idâ‚› t)

âŸ¨-âŸ©-unit-left : {a : Ty} (b : Ty) (t : a âŠ¢ b) â†’ id âŸ¨ t âŸ© â‰ˆ t
âŸ¨-âŸ©-unit-left _ _ = â‰ˆ-refl

âŸ¨-âŸ©-assoc : (t : c âŠ¢ d) (u : b âŠ¢ c) (u' : a âŠ¢ b) â†’ (t âŸ¨ u âŸ©) âŸ¨ u' âŸ© â‰ˆ t âŸ¨ u âŸ¨ u' âŸ© âŸ©
âŸ¨-âŸ©-assoc t u u' = â‰¡-to-â‰ˆ ((â‰¡-sym (substTm-pres-âˆ™â‚› [ u ]â‚› [ u' ]â‚› t)))

ğ’¯ : Categoryâ‚€
ğ’¯ = record
  { Obj          = Ty
  ; _â†’Ì‡_          = _âŠ¢_
  ; _â‰ˆÌ‡_          = _â‰ˆ_
  ; _âˆ˜_          = _âŸ¨_âŸ©
  ; id'[_]       = Î» _ â†’ id
  ; â‰ˆÌ‡-refl       = â‰ˆ-refl
  ; â‰ˆÌ‡-sym        = â‰ˆ-sym
  ; â‰ˆÌ‡-trans      = â‰ˆ-trans
  ; âˆ˜-pres-â‰ˆÌ‡     = âŸ¨-âŸ©-pres-â‰ˆ
  ; âˆ˜-unit-left  = âŸ¨-âŸ©-unit-left
  ; âˆ˜-unit-right = âŸ¨-âŸ©-unit-right
  ; âˆ˜-assoc      = âŸ¨-âŸ©-assoc
  }

--
-- term model is a cartesian category
--

Ï€â‚ : (a Ã— b) âŠ¢ a
Ï€â‚ = fst v0â‚œ

Ï€â‚‚ : (a Ã— b) âŠ¢ b
Ï€â‚‚ = snd v0â‚œ

ğ’¯-is-CC : IsCartesianâ‚— ğ’¯
ğ’¯-is-CC = record
  { []'           = ğŸ™
  ; unit'         = unit
  ; []'-eta       = exp-unit _
  ; _Ã—'_          = _Ã—_
  ; âŸ¨_,_âŸ©'        = pair
  ; âŸ¨,âŸ©'-pres-â‰ˆÌ‡   = cong-pair
  ; Ï€â‚'[_]        = Î» _ â†’ Ï€â‚
  ; Ï€â‚‚'[_]        = Î» _ â†’ Ï€â‚‚
  ; Ã—'-beta-left  = Î» t â†’ red-prod1 _ t
  ; Ã—'-beta-right = Î» u â†’ red-prod2 u _
  ; Ã—'-eta        = exp-prod _
  }

--
-- term model is cartesian closed
--

prâ‚› : Sub ([ a ] `, b) [ a Ã— b ]
prâ‚› = [ pair v1â‚œ v0â‚œ ]â‚›

curry : (a Ã— b) âŠ¢ c â†’ a âŠ¢ (b â‡’ c)
curry t = lam (substTm prâ‚› t)

â‡’-beta : (t : (a Ã— b) âŠ¢ c) (u : a âŠ¢ b)
  â†’ app (curry t) u â‰ˆ t âŸ¨ pair id u âŸ©
â‡’-beta t u = â‰ˆ-trans (red-fun _ _) (â‰¡-to-â‰ˆ (â‰¡-sym (substTm-pres-âˆ™â‚› _ _ t)))

wkFreshLemma : (t : a âŠ¢ b) â†’ wkTm freshWk[ _ , c ] t â‰ˆ substTm prâ‚› (t âŸ¨ Ï€â‚ âŸ©)
wkFreshLemma t = let open EqReasoning (Tm-setoid _ _) in begin
  wkTm freshWk t
    â‰¡Ë˜âŸ¨ cong (wkTm freshWk) (substTm-pres-idâ‚› t) âŸ©
  wkTm freshWk (substTm idâ‚› t)
    â‰¡âŸ¨âŸ©
  wkTm freshWk (substTm [ v0â‚œ ]â‚› t)
    â‰¡Ë˜âŸ¨ substTm-nat t [ v0â‚œ ]â‚› freshWk âŸ©
  substTm (wkSub freshWk [ v0â‚œ ]â‚›) t
    â‰¡âŸ¨âŸ©
  substTm [ v1â‚œ ]â‚› t
    â‰ˆË˜âŸ¨ substTm-pres-â‰ˆ-left t ([-]â‚›-pres-â‰ˆ (red-prod1 _ _)) âŸ©
  substTm ([ Ï€â‚ ]â‚› âˆ™â‚› prâ‚›) t
    â‰¡âŸ¨ substTm-pres-âˆ™â‚› _ _ t âŸ©
  substTm prâ‚› (t âŸ¨ Ï€â‚ âŸ©) âˆ

â‡’-eta : (t : a âŠ¢ (b â‡’ c)) â†’ t â‰ˆ curry (app (t âŸ¨ Ï€â‚ âŸ©) Ï€â‚‚)
â‡’-eta t = â‰ˆ-trans (exp-fun t) (cong-lam (cong-app (wkFreshLemma t) (â‰ˆ-sym (red-prod2 _ _))))

open IsCartesian ğ’¯-is-CC renaming
  (_Ã—'-map_  to _Ã—-map_
  ; Ã—'-assoc to Ã—-assoc)

curry-nat : (t : (b Ã— c) âŠ¢ d) (u : a âŠ¢ b) â†’ curry t âŸ¨ u âŸ© â‰ˆ curry (t âŸ¨ u Ã—-map id âŸ©)
curry-nat t u = cong-lam lemma
  where
  lemma : substTm (keepâ‚› [ u ]â‚›) (substTm prâ‚› t) â‰ˆ substTm prâ‚› (t âŸ¨ u Ã—-map id âŸ©)
  lemma = let open EqReasoning (Tm-setoid _ _) in begin
    substTm (keepâ‚› [ u ]â‚›) (substTm prâ‚› t)
      â‰¡Ë˜âŸ¨ substTm-pres-âˆ™â‚› _ _ t âŸ©
    substTm (prâ‚› âˆ™â‚› keepâ‚› [ u ]â‚›) t
      â‰¡âŸ¨âŸ©
    substTm [ pair (wkTm freshWk u) v0â‚œ ]â‚› t
      â‰ˆâŸ¨ substTm-pres-â‰ˆ-left t ([-]â‚›-pres-â‰ˆ (cong-pair
          (wkFreshLemma u)
          (â‰ˆ-sym (red-prod2 _ _)))) âŸ©
    substTm ([ pair (substTm prâ‚› (u âŸ¨ Ï€â‚ âŸ©)) (snd (pair _ v0â‚œ)) ]â‚›) t
      â‰¡âŸ¨âŸ©
    substTm ([ u Ã—-map id ]â‚› âˆ™â‚› prâ‚›) t
      â‰¡âŸ¨  substTm-pres-âˆ™â‚› _ _ t âŸ©
    substTm prâ‚› (t âŸ¨ u Ã—-map id âŸ©) âˆ

ğ’¯-is-CCC : IsCartesianClosedâ‚— ğ’¯ ğ’¯-is-CC
ğ’¯-is-CCC = record
  { _â‡’'_        = _â‡’_
  ; lam'        = curry
  ; lam'-pres-â‰ˆÌ‡ = Î» tâ‰ˆt' â†’ cong-lam (substTm-pres-â‰ˆ-right _ tâ‰ˆt')
  ; app'        = app
  ; app'-pres-â‰ˆÌ‡ = cong-app
  ; â‡’'-beta     = â‡’-beta
  ; â‡’'-eta      = â‡’-eta
  ; lam'-nat    = curry-nat
  ; app'-nat    = Î» _ _ _ â†’ â‰ˆ-refl
  }

--
-- â—‡ is a functor
--

â—‡-map : a âŠ¢ b â†’ (â—‡ a) âŠ¢ (â—‡ b)
â—‡-map t = sletin v0â‚œ (wkTm (keep freshWk) t)

â—‡-map-pres-â‰ˆ : t â‰ˆ t' â†’ â—‡-map t â‰ˆ â—‡-map t'
â—‡-map-pres-â‰ˆ tâ‰ˆt' = cong-sletin2 (wkTm-pres-â‰ˆ (keep freshWk) tâ‰ˆt')

â—‡-map-pres-âŠ¢refl : â—‡-map id[ a ] â‰ˆ id[ â—‡ a ]
â—‡-map-pres-âŠ¢refl = â‰ˆ-sym (exp-dia v0â‚œ)

â—‡-map-pres-âŸ¨-âŸ© : (t : b âŠ¢ c) (u : a âŠ¢ b) â†’ â—‡-map (t âŸ¨ u âŸ©) â‰ˆ (â—‡-map t âŸ¨ â—‡-map u âŸ© )
â—‡-map-pres-âŸ¨-âŸ© t u = let open EqReasoning (Tm-setoid _ _) in begin
  -- Agda's normalization is doing a lot in this proof;
  -- the details of which are noisy, and thus hidden.
  sletin v0â‚œ (wkTm _ (substTm [ u ]â‚› t))
    â‰¡Ë˜âŸ¨ cong (sletin _) (substTm-nat t _ _) âŸ©
  sletin v0â‚œ (substTm (wkSub _ [ u ]â‚› ) t)
    â‰¡âŸ¨ cong (sletin _) (substTm-pres-âˆ™â‚› _ _ t) âŸ©
  sletin v0â‚œ (substTm _{-u-} (substTm _ t))
    â‰ˆË˜âŸ¨ red-dia1 _ _ _ âŸ©
  sletin (sletin v0â‚œ _{-u-}) (substTm _ t)
    â‰¡âŸ¨ cong (sletin _) (assoc-substTm-trimSub t _ _) âŸ©
  sletin v0â‚œ (wkTm _ t) âŸ¨ sletin v0â‚œ (wkTm _ u) âŸ©
    âˆ

â—‡â„± : EndoFunctorâ‚— ğ’¯
â—‡â„± = record
  { â„±'_         = â—‡_
  ; map_        = â—‡-map
  ; map-pres-â‰ˆÌ‡  = â—‡-map-pres-â‰ˆ
  ; map-pres-id = â—‡-map-pres-âŠ¢refl
  ; map-pres-âˆ˜  = â—‡-map-pres-âŸ¨-âŸ©
  }

--
-- â—‡ is a strong functor
--

â—‡-strength[_,_] : (a b : Ty) â†’ (a Ã— â—‡ b) âŠ¢ â—‡ (a Ã— b)
â—‡-strength[ _ , _ ] = sletin (snd v0â‚œ) (pair (fst v1â‚œ) v0â‚œ)

â—‡-strength : (a Ã— â—‡ b) âŠ¢ â—‡ (a Ã— b)
â—‡-strength = â—‡-strength[ _ , _ ]

â—‡-strength-naturalâ‚ : (t : a âŠ¢ b)
  â†’ â—‡-strength âŸ¨ t Ã—-map id[ â—‡ c ] âŸ© â‰ˆ â—‡-map (t Ã—-map id[ c ]) âŸ¨ â—‡-strength âŸ©
â—‡-strength-naturalâ‚ t = let open EqReasoning (Tm-setoid _ _) in begin
  sletin (snd (pair _ _)) (pair (fst (pair _ _)) _)
    â‰ˆâŸ¨ cong-sletin (red-prod2 _ _) (cong-pair1 (red-prod1 _ _)) âŸ©
  sletin _ (pair (wkTm freshWk (t âŸ¨ Ï€â‚ âŸ©)) _)
    â‰ˆâŸ¨ cong-sletin2 (cong-pair1 (wkFreshLemma (t âŸ¨ Ï€â‚ âŸ©))) âŸ©
  sletin _ (pair (substTm prâ‚› (t âŸ¨ Ï€â‚ âŸ© âŸ¨ Ï€â‚ âŸ©)) _)
    â‰¡Ë˜âŸ¨ cong (Î» z â†’ sletin _ (pair z _))
          (â‰¡-trans
            (substTm-pres-âˆ™â‚› _ _ t)
            (substTm-pres-âˆ™â‚› _ _ (t âŸ¨ Ï€â‚ âŸ©))) âŸ©
  sletin Ï€â‚‚ (pair (substTm [ fst (fst (pair _ _)) ]â‚› t) _)
    â‰ˆâŸ¨ cong-sletin2 (cong-pair1
         (substTm-pres-â‰ˆ-left t
           ([-]â‚›-pres-â‰ˆ (cong-fst (red-prod1 _ _))))) âŸ©
  sletin Ï€â‚‚ (pair (substTm [ fst _ ]â‚› t) v0â‚œ)
    â‰ˆË˜âŸ¨ cong-sletin2 (cong-pair1
          (substTm-pres-â‰ˆ-left t ([-]â‚›-pres-â‰ˆ (red-prod1 _ _)))) âŸ©
  sletin Ï€â‚‚ (pair (substTm [ fst (pair (fst _) _) ]â‚› t) _)
    â‰ˆË˜âŸ¨ cong-sletin2 (cong-pair
          (â‰¡-to-â‰ˆ (â‰¡-sym (substTm-pres-âˆ™â‚› _ _ t)))
          (red-prod2 _ _)) âŸ©
  sletin Ï€â‚‚ (pair (substTm _ (substTm [ _ ]â‚› t)) _)
    â‰ˆË˜âŸ¨ red-dia1 Ï€â‚‚ _ _ âŸ©
  sletin (sletin _ _) (pair (substTm [ _ ]â‚› t) _)
    â‰¡âŸ¨ cong (Î» z â†’ sletin _ (pair z _))
         (substTm-pres-âˆ™â‚› _ _ t) âŸ©
  sletin (sletin _ _) (pair (substTm [ _ ]â‚› (t âŸ¨ Ï€â‚ âŸ©)) _)
    â‰¡âŸ¨ cong (Î» z â†’ sletin _ (pair z _))
       (assoc-substTm-trimSub (t âŸ¨ Ï€â‚ âŸ©) _ _) âŸ©
  sletin (sletin _ _) (pair (substTm _ (wkTm _ (t âŸ¨ Ï€â‚ âŸ©))) _)
    âˆ

â—‡-strength-naturalâ‚‚ : (t : b âŠ¢ c)
  â†’ â—‡-strength âŸ¨ id[ a ] Ã—-map (â—‡-map t) âŸ© â‰ˆ â—‡-map (id[ a ] Ã—-map t) âŸ¨ â—‡-strength âŸ©
â—‡-strength-naturalâ‚‚ t = let open EqReasoning (Tm-setoid _ _) in begin
  sletin (snd (pair _ _)) (pair (fst (pair _ _)) _)
    â‰ˆâŸ¨ cong-sletin (red-prod2 _ _) (cong-pair1 (red-prod1 _ _)) âŸ©
  sletin (sletin Ï€â‚‚ (substTm _ (wkTm _ t))) (pair _ _)
    â‰¡Ë˜âŸ¨ cong (Î» z â†’ sletin (sletin _ z ) (pair _ _)) (assoc-substTm-wkTm t _ _) âŸ©
  sletin (sletin Ï€â‚‚ (substTm _ t)) (pair _ _)
    â‰ˆâŸ¨ red-dia1 _ _ _ âŸ©
  sletin Ï€â‚‚ (pair _ (substTm [ v0â‚œ ]â‚› t))
    â‰ˆË˜âŸ¨ cong-sletin2 (cong-pair2 (substTm-pres-â‰ˆ-left t ([-]â‚›-pres-â‰ˆ (red-prod2 _ _)))) âŸ©
  sletin Ï€â‚‚ (pair _ (substTm [ (snd (pair _ _)) ]â‚› t))
    â‰ˆË˜âŸ¨ cong-sletin2 (cong-pair (red-prod1 _ _) (â‰¡-to-â‰ˆ (â‰¡-sym (substTm-pres-âˆ™â‚› _ _ t)))) âŸ©
  sletin Ï€â‚‚ (pair (fst (pair _ _)) (substTm _ (substTm _ t)))
    â‰ˆË˜âŸ¨ red-dia1 _ _ _ âŸ©
  sletin (sletin _ _) (pair _ (substTm [ _ ]â‚› t))
    â‰¡âŸ¨ cong (Î» z â†’ sletin _ (pair _ z)) (substTm-pres-âˆ™â‚› _ _ t) âŸ©
  sletin _ (pair _ (substTm _ (substTm _ t)))
    â‰¡âŸ¨ cong (Î» z â†’ sletin _ (pair _ (substTm _ z))) (substTm-nat t _ _) âŸ©
  sletin _ (pair _ (substTm _ (wkTm _ (substTm _ t))))
    âˆ

â—‡-strength-unit : â—‡-map Ï€â‚‚ âŸ¨ â—‡-strength[ a , b ] âŸ© â‰ˆ Ï€â‚‚
â—‡-strength-unit = let open EqReasoning (Tm-setoid _ _) in begin
  sletin (sletin Ï€â‚‚ (pair _ _)) (snd v0â‚œ)
    â‰ˆâŸ¨ red-dia1 _ _ _ âŸ©
  sletin Ï€â‚‚ (snd (pair _ _))
    â‰ˆâŸ¨ cong-sletin2 (red-prod2 _ _) âŸ©
  sletin Ï€â‚‚ _
    â‰ˆË˜âŸ¨ exp-dia _ âŸ©
  Ï€â‚‚ âˆ

â—‡-strength-assoc : (â—‡-map Ã—-assoc) âŸ¨ â—‡-strength[ a Ã— b , c ] âŸ©
  â‰ˆ (â—‡-strength âŸ¨ id Ã—-map (â—‡-strength) âŸ© âŸ¨ Ã—-assoc âŸ©)
â—‡-strength-assoc = let open EqReasoning (Tm-setoid _ _) in begin
  sletin (sletin _ (pair _ _)) (pair _ _)
    â‰ˆâŸ¨ red-dia1 _ _ _ âŸ©
  sletin Ï€â‚‚ (pair
      (fst (fst (pair _ _)))
      (pair (snd (fst (pair _ _))) (snd (pair _ _))))
    â‰ˆâŸ¨ cong-sletin2 (cong-pair
        (cong-fst (red-prod1 _ _))
        (cong-pair
          (cong-snd (red-prod1 _ _))
          (red-prod2 _ _))) âŸ©
  sletin Ï€â‚‚ (pair _ (pair _ _))
    â‰ˆË˜âŸ¨ red-dia1 _ _ _ âŸ©
  sletin (sletin _ _) (pair _ _)
    â‰ˆË˜âŸ¨ cong-sletin (cong-sletin
          (â‰ˆ-trans (cong-snd (red-prod2 _ _)) (red-prod2 _ _))
          (cong-pair1 (â‰ˆ-trans (cong-fst (red-prod2 _ _)) (red-prod1 _ _))))
          (cong-pair1 (red-prod1 _ _)) âŸ©
  sletin
   (sletin (snd (snd (pair _ (pair _ _))))
   (pair (fst (snd (pair _ (pair _ _)))) _)) _
    â‰ˆË˜âŸ¨ cong-sletin
          (red-prod2 _ _)
          (cong-pair1 (red-prod1 _ _)) âŸ©
  sletin (snd (pair _ _)) (pair (fst (pair _ _ )) _)
    âˆ

â—‡-is-strong : IsStrongâ‚— ğ’¯-is-CC â—‡â„±
â—‡-is-strong = record
   { strength[_,_]     = Î» _ _ â†’ â—‡-strength -- use implicit version for smaller goals
   ; strength-naturalâ‚ = â—‡-strength-naturalâ‚
   ; strength-naturalâ‚‚ = â—‡-strength-naturalâ‚‚
   ; strength-assoc    = â—‡-strength-assoc
   ; strength-unit     = â—‡-strength-unit
   }

--
-- â—‡ is joinable
--

â—‡-join[_] : (a : Ty) â†’ â—‡ â—‡ a âŠ¢ â—‡ a
â—‡-join[ _ ] = jletin id v0â‚œ

â—‡-join : â—‡ â—‡ a âŠ¢ â—‡ a
â—‡-join = â—‡-join[ _ ]

â—‡-join-natural : (t : a âŠ¢ b)
  â†’ â—‡-join[ b ] âŸ¨ â—‡-map (â—‡-map t) âŸ© â‰ˆ â—‡-map t âŸ¨ â—‡-join[ a ] âŸ©
â—‡-join-natural {a} {b} t = let open EqReasoning (Tm-setoid _ _) in begin
  jletin (sletin id (sletin v0â‚œ (wkTm _ (wkTm _ t)))) v0â‚œ
    â‰¡Ë˜âŸ¨ cong (Î» z â†’ jletin (sletin _ (sletin _ z)) _) (wkTm-pres-âŠ†-trans _ _ t) âŸ©
  jletin (sletin v0â‚œ (sletin v0â‚œ (wkTm _ t))) v0â‚œ
    -- sletin elim
    â‰ˆâŸ¨ red-dia2 v0â‚œ _ v0â‚œ âŸ©
  jletin id (sletin v0â‚œ (wkTm _ t))
    â‰ˆË˜âŸ¨ cong-jletin2 (cong-sletin2 (wkTm-pres-â‰ˆ _ (â‰¡-to-â‰ˆ (substTm-pres-idâ‚› t)))) âŸ©
  jletin id (sletin v0â‚œ (wkTm _ (substTm idâ‚› t)))
    â‰¡Ë˜âŸ¨ cong (Î» z â†’ jletin v0â‚œ (sletin v0â‚œ z)) (substTm-nat t _ _) âŸ©
  jletin id (sletin v0â‚œ (substTm [ v0â‚œ ]â‚› t))
    â‰¡âŸ¨ cong (Î» z â†’ jletin v0â‚œ (sletin v0â‚œ z)) (substTm-nat t _ _) âŸ©
  jletin id (sletin v0â‚œ (wkTm (keep freshWk) (substTm [ v0â‚œ ]â‚› t)))
    -- jletin-sletin commute
    â‰ˆË˜âŸ¨ com-dia v0â‚œ v0â‚œ _ âŸ©
  sletin (jletin id v0â‚œ) (substTm [ v0â‚œ ]â‚› t)
    â‰¡âŸ¨ cong (Î» z â†’ sletin _ z) (assoc-substTm-wkTm t _ _)  âŸ©
  sletin (jletin id v0â‚œ) (substTm _ (wkTm _ t))
    âˆ

â—‡-join-assoc : â—‡-join[ a ] âŸ¨ â—‡-map (â—‡-join[ a ]) âŸ© â‰ˆ â—‡-join[ a ] âŸ¨ â—‡-join[ â—‡  a ] âŸ©
â—‡-join-assoc = let open EqReasoning (Tm-setoid _ _) in begin
  jletin (sletin v0â‚œ (jletin v0â‚œ v0â‚œ)) v0â‚œ
    -- sletin elim
    â‰ˆâŸ¨ red-dia2 _ _ _ âŸ©
  jletin v0â‚œ (jletin v0â‚œ v0â‚œ)
    â‰ˆË˜âŸ¨ ass-dia _ _ _ âŸ©
  jletin (jletin v0â‚œ v0â‚œ) v0â‚œ
    âˆ

â—‡-is-joinable : IsMultiplicativeâ‚— â—‡â„±
â—‡-is-joinable = record
  { mult[_]      = Î» a â†’ â—‡-join
  ; mult-natural = â—‡-join-natural
  ; mult-assoc   = â—‡-join-assoc
  }

--
-- â—‡ is strong joinable
--

â—‡-strength-mult : â—‡-join[ a Ã— b ] âŸ¨ â—‡-map (â—‡-strength) âŸ¨  â—‡-strength âŸ© âŸ© â‰ˆ â—‡-strength[ a , b ] âŸ¨ (id[ a ] Ã—-map â—‡-join[ b ]) âŸ©
â—‡-strength-mult = let open EqReasoning (Tm-setoid _ _) in begin
  jletin (sletin (sletin _ _) _) v0â‚œ
    â‰ˆâŸ¨ red-dia2 _ _ _ âŸ©
  jletin (sletin _ _) _
    â‰ˆâŸ¨ red-dia2 _ _ _ âŸ©
  jletin _ (sletin (snd (pair _ _)) (pair (fst (pair _ _)) _))
    â‰ˆâŸ¨ cong-jletin2 (cong-sletin (red-prod2 _ _) (cong-pair1 (red-prod1 _ _))) âŸ©
  jletin _ (sletin _ _)
    â‰ˆË˜âŸ¨ com-dia _ _ _ âŸ©
  sletin (jletin _ _) (pair _ v0â‚œ)
    â‰ˆË˜âŸ¨ cong-sletin (red-prod2 _ _) (cong-pair1 (red-prod1 _ _)) âŸ©
  sletin (snd (pair _ _)) (pair (fst (pair _ _)) v0â‚œ)
    âˆ

â—‡-is-strong-joinable : IsStrongMultiplicativeâ‚— â—‡â„± â—‡-is-strong â—‡-is-joinable
â—‡-is-strong-joinable = record { strength-mult = â—‡-strength-mult }

--
-- categorical completeness machinery
--

-- will be replaced by evalCtx
âŸ¦_âŸ§ : Ctx â†’ Ty
âŸ¦ [] âŸ§     = ğŸ™
âŸ¦ Î“ `, a âŸ§ = âŸ¦ Î“ âŸ§ Ã— a

-- "context term" (c.f. Lemma 3.1 in [Clouston 2018])
câ‚œ[_] : âˆ€ Î“ â†’ Tm Î“ âŸ¦ Î“ âŸ§
câ‚œ[ [] ]     = unit
câ‚œ[ Î“ `, a ] = pair (wkTm freshWk câ‚œ[ Î“ ]) v0â‚œ

from-âŠ¢ : âŸ¦ Î“ âŸ§ âŠ¢ a â†’ Tm Î“ a
from-âŠ¢ = substTm [ câ‚œ[ _ ] ]â‚›

from-âŠ¢-pres-â‰ˆ : {t' u' : âŸ¦ Î“ âŸ§ âŠ¢ a} â†’ t' â‰ˆ u' â†’ from-âŠ¢ t' â‰ˆ from-âŠ¢ u'
from-âŠ¢-pres-â‰ˆ = substTm-pres-â‰ˆ-right _
