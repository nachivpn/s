{-# OPTIONS --safe --without-K #-}

module PFC.Norm.Base where

open import Data.Product using (âˆƒ; _Ã—_; _,_; -,_) renaming (projâ‚ to fst; projâ‚‚ to snd)
open import Data.Product.Properties using ()

open import Relation.Binary.PropositionalEquality
  using    (_â‰¡_ ; cong ; congâ‚‚ ; module â‰¡-Reasoning ; subst ; substâ‚‚)
  renaming (refl to â‰¡-refl ; sym to â‰¡-sym ; trans to â‰¡-trans ; isEquivalence to â‰¡-equiv)
import Relation.Binary.Reasoning.Setoid as EqReasoning

open import Data.Product using (âˆƒ; _Ã—_; _,_; -,_) renaming (projâ‚ to fst; projâ‚‚ to snd)
open import Data.Product using () renaming (âˆƒ to Î£; _Ã—_ to _âˆ§_)

open import PUtil using (Î£Ã—-â‰¡,â‰¡,â‰¡â†’â‰¡Ë˜)
open import PEUtil using (subst-applicationâ€²)

open import PFC.Term
open import PFC.Term.Conversion
open import PFC.Term.NormalForm
open import PFC.Term.NormalForm.Properties

open import Semantics.Kripke.Frame

data _âŠ²_ : Ctx â†’ Ctx â†’ Set where
  nil    : Î“ âŠ² Î“
  single : Ne Î“ (â—‡ a) â†’ Î“ âŠ² (Î“ `, a)

factor : Î“ âŠ† Î“' â†’ Î“ âŠ² Î” â†’ âˆƒ (Î» Î”' â†’ (Î“' âŠ² Î”') âˆ§ Î” âŠ† Î”')
factor i nil        = (-, nil , i)
factor w (single n) = (-, single (wkNe w n) , keep w)

factorC : (w : Î“ âŠ† Î“') (m : Î“ âŠ² Î”) â†’ Ctx
factorC w m = factor w m .fst

factorâŠ² : (w : Î“ âŠ† Î“') (m : Î“ âŠ² Î”) â†’ Î“' âŠ² _
factorâŠ²  w m = factor w m .snd .fst

factorâŠ† : (w : Î“ âŠ† Î“') (m : Î“ âŠ² Î”) â†’ Î” âŠ† _
factorâŠ† w m = factor w m .snd .snd

factor-is-a-triple : (w : Î“ âŠ† Î“') (m : Î“ âŠ² Î”) â†’ factor w m â‰¡ (factorC w m , factorâŠ² w m , factorâŠ†  w m)
factor-is-a-triple w m = â‰¡-refl

factor-pres-âŠ†-refl : (m : Î“ âŠ² Î”) â†’ factor âŠ†-refl m â‰¡ (-, m , âŠ†-refl)
factor-pres-âŠ†-refl nil        = â‰¡-refl
factor-pres-âŠ†-refl (single n) rewrite wkNe-pres-âŠ†-refl n = â‰¡-refl

factor-pres-âŠ†-trans : (w : Î“ âŠ† Î“') (w' : Î“' âŠ† Î“'') (m : Î“ âŠ² Î”)
  â†’ factor (w âˆ™ w') m â‰¡ (-, (factorâŠ² w' (factorâŠ² w m) , ((factorâŠ† w m) âˆ™ (factorâŠ† w' (factorâŠ² w m)))))
factor-pres-âŠ†-trans w w' nil = â‰¡-refl
factor-pres-âŠ†-trans w w' (single m) rewrite wkNe-pres-âŠ†-trans w w' m = â‰¡-refl

âŠ²-to-âŠ† : Î“ âŠ² Î” â†’ Î“ âŠ† Î”
âŠ²-to-âŠ† nil                = âŠ†-refl
âŠ²-to-âŠ† (single {a = a} n) = freshWk[ _ , a ]

MF : MFrame ğ’²  _âŠ²_
MF = record
      { factor              = factor
      ; factor-pres-âŠ†-refl  = factor-pres-âŠ†-refl
      ; factor-pres-âŠ†-trans = factor-pres-âŠ†-trans
      }

factor-pres-R-to-âŠ† : (w : Î“ âŠ† Î“') (m : Î“ âŠ² Î”) â†’ w âˆ™ (âŠ²-to-âŠ† (factorâŠ² w m)) â‰¡ (âŠ²-to-âŠ† m) âˆ™ (factorâŠ† w m)
factor-pres-R-to-âŠ† w nil        = â‰¡-trans (âŠ†-trans-unit-right w) (â‰¡-sym (âŠ†-trans-unit-left w))
factor-pres-R-to-âŠ† w (single n) = freshWk-natural w

IMF : InclusiveMFrame MF
IMF = record { R-to-âŠ† = âŠ²-to-âŠ† ; factor-pres-R-to-âŠ† = factor-pres-R-to-âŠ† }

âŠ²-refl : Î“ âŠ² Î“
âŠ²-refl = nil

RMF : ReflexiveMFrame MF
RMF = record { R-refl = âŠ²-refl ; factor-pres-R-refl = Î» i â†’ â‰¡-refl }

IRMF : InclusiveReflexiveMFrame MF IMF RMF
IRMF = record { R-to-âŠ†-pres-refl = â‰¡-refl }

open import Semantics.Presheaf.Base ğ’² public
open import Semantics.Presheaf.CartesianClosure ğ’² public
open import Semantics.Presheaf.Possibility.Base MF public
open import Semantics.Presheaf.Possibility.Strong.Base MF IMF public
open import Semantics.Presheaf.Possibility.Pointed MF RMF public
open import Semantics.Presheaf.Possibility.Strong.Pointed MF IRMF public

Ne'- : Ty â†’ Psh
Ne'- a = record
          { Fam           = Î» Î“ â†’ Ne Î“ a
          ; _â‰‹_           = _â‰¡_
          ; â‰‹-equiv       = Î» _ â†’ â‰¡-equiv
          ; wk            = wkNe
          ; wk-pres-â‰‹     = Î» w â†’ cong (wkNe w)
          ; wk-pres-refl  = wkNe-pres-âŠ†-refl
          ; wk-pres-trans = wkNe-pres-âŠ†-trans
          }

Nf'- : Ty â†’ Psh
Nf'- a = record
          { Fam           = Î» Î“ â†’ Nf Î“ a
          ; _â‰‹_           = _â‰¡_
          ; â‰‹-equiv       = Î» _ â†’ â‰¡-equiv
          ; wk            = wkNf
          ; wk-pres-â‰‹     = Î» w â†’ cong (wkNf w)
          ; wk-pres-refl  = wkNf-pres-âŠ†-refl
          ; wk-pres-trans = wkNf-pres-âŠ†-trans
          }

open import PFC.Evaluation PshCat PshCat-is-CCC â—‡'-is-PshFunctor â—‡'-is-strong-pointed (Ne'- Î¹)

open Eval (Ne'- Î¹) hiding (Sub' ; Tm')

-- interpretation of types
Ty'- : (a : Ty) â†’ Psh
Ty'- = evalTy

Ty' : Ctx â†’ Ty â†’ Set
Ty' Î“ a = Ty'- a â‚€ Î“

-- interpretation of contexts
Sub'- : (Î“ : Ctx) â†’ Psh
Sub'- = evalCtx

Sub' : Ctx â†’ Ctx â†’ Set
Sub' Î“ Î” = Sub'- Î” â‚€ Î“

-- interpretation of terms
eval : Tm Î“ a â†’ (Sub'- Î“ â†’Ì‡ Ty'- a)
eval = evalTm

register-fun : Ne Î“ (â—‡ a) â†’ â—‡' (Ne'- a) â‚€ Î“
register-fun n = elem (_ , single n , var zero)

register-natural : Natural (Ne'- (â—‡ a)) (â—‡' (Ne'- a)) register-fun
register-natural w p = proof (â‰¡-refl , (â‰¡-refl , â‰¡-refl))

register : Ne'- (â—‡ a) â†’Ì‡ â—‡' (Ne'- a)
register = record
  { fun     = register-fun
  ; pres-â‰‹  = Î» pâ‰‹p' â†’ proof (â‰¡-refl , cong single pâ‰‹p' , â‰¡-refl)
  ; natural = register-natural
  }

collectNf-fun : (â—‡' Nf'- a) â‚€ Î“ â†’ Nf'- (â—‡ a) â‚€ Î“
collectNf-fun (elem (Î” , nil , n0))      = return n0
collectNf-fun (elem (Î” , single n , n0)) = letin n n0

collectNf-pres-â‰‹ : Pres-â‰‹ (â—‡' (Nf'- a)) (Nf'- (â—‡ a)) collectNf-fun
collectNf-pres-â‰‹ (proof (â‰¡-refl , â‰¡-refl , â‰¡-refl)) = â‰¡-refl

collectNf-natural : Natural (â—‡' (Nf'- a)) (Nf'- (â—‡ a)) collectNf-fun
collectNf-natural w (elem (Î” , nil , n))       = â‰¡-refl
collectNf-natural w (elem (Î” , single n , n0)) = â‰¡-refl

collectNf : â—‡' (Nf'- a) â†’Ì‡ Nf'- (â—‡ a)
collectNf = record
  { fun     = collectNf-fun
  ; pres-â‰‹  = collectNf-pres-â‰‹
  ; natural = collectNf-natural
  }

module _ where

  reflect-fun     : (a : Ty) â†’ Ne  Î“ a â†’ Ty' Î“ a
  reflect-pres-â‰‹  : (a : Ty) â†’ Pres-â‰‹ (Ne'- a) (Ty'- a) (reflect-fun a)
  reflect-natural : (a : Ty) â†’ Natural (Ne'- a) (Ty'- a) (reflect-fun a)

  reify-fun     : (a : Ty) â†’ Ty' Î“ a â†’ Nf Î“ a
  reify-pres-â‰‹  : (a : Ty) â†’ Pres-â‰‹ (Ty'- a) (Nf'- a) (reify-fun a)
  reify-natural : (a : Ty) â†’ Natural (Ty'- a) (Nf'- a) (reify-fun a)

  reflect-fun Î¹       n = n
  reflect-fun (a â‡’ b) n = record
    { fun     = Î» w    p    â†’ reflect-fun b (app (wkNe w n) (reify-fun a p))
    ; pres-â‰‹  = Î» w    pâ‰‹p' â†’ reflect-pres-â‰‹ b (cong (app (wkNe w n)) (reify-pres-â‰‹ a pâ‰‹p'))
    ; natural = Î» w w' p    â†’ let open EqReasoning â‰‹[ evalTy b ]-setoid in begin
      wk[ evalTy b ] w' (reflect-fun b (app (wkNe w n) (reify-fun a p)))            â‰ˆâŸ¨ reflect-natural b w' _ âŸ©
      reflect-fun b (wkNe w' (app (wkNe w n) (reify-fun a p)))                      â‰¡âŸ¨âŸ©
      reflect-fun b (app (wkNe w' (wkNe w n)) (wkNf w' (reify-fun a p)))            â‰¡âŸ¨ cong (Î» m â†’ reflect-fun b (app _ m)) (reify-natural a w' p) âŸ©
      reflect-fun b (app (wkNe w' (wkNe w n)) (reify-fun a (wk[ evalTy a ] w' p)))  â‰¡âŸ¨ cong (Î» n â†’ reflect-fun b (app n _)) (â‰¡-sym (wkNe-pres-âŠ†-trans w w' n)) âŸ©
      reflect-fun b (app (wkNe (w âˆ™ w') n) (reify-fun a (wk[ evalTy a ] w' p)))     âˆ
    }
  reflect-fun (â—‡ a)   n = â—‡'-map-fun (reflect-fun a) (register-fun n)

  reify-fun Î¹         n  = up  n
  reify-fun (a â‡’ b)   f  = lam (reify-fun b (f .apply freshWk (reflect-fun a (var zero))))
  reify-fun (â—‡ a)     x  = collectNf-fun (â—‡'-map-fun (reify-fun a) x)

  reflect-pres-â‰‹  = Î» a nâ‰¡n' â†’ â‰‹[ evalTy a ]-reflexive (cong (reflect-fun a) nâ‰¡n')

  reflect-natural Î¹       w n = â‰‹[ evalTy Î¹ ]-refl
  reflect-natural (a â‡’ b) w n = record
    { pw = Î» w' p â†’ let open EqReasoning â‰‹[ evalTy b ]-setoid in begin
       wk[ evalTy (a â‡’ b) ] w (reflect-fun (a â‡’ b) n) .apply w' p
          â‰¡âŸ¨âŸ©
       reflect-fun b (app (wkNe (w âˆ™ w') n) (reify-fun a p))
         â‰¡Ë˜âŸ¨ cong (Î» n â†’ reflect-fun b (app n (reify-fun a p))) (â‰¡-sym (wkNe-pres-âŠ†-trans w w' n)) âŸ©
       reflect-fun b (app (wkNe w' (wkNe w n)) (reify-fun a p))
         â‰¡âŸ¨âŸ©
       reflect-fun (a â‡’ b) (wkNe w n) .apply w' p âˆ
    }
  reflect-natural (â—‡ a) w n = â—‡'-map-natural (reflect-natural a) w (register-fun n)

  reify-pres-â‰‹ Î¹       xâ‰‹x' = cong up  xâ‰‹x'
  reify-pres-â‰‹ (a â‡’ b) xâ‰‹x' = cong lam (reify-pres-â‰‹ b (xâ‰‹x' .pw freshWk[ _ , a ] _))
  reify-pres-â‰‹ (â—‡ a)   xâ‰‹x' = collectNf-pres-â‰‹ (â—‡'-map-fun-pres-â‰‹ (reify-pres-â‰‹ a) xâ‰‹x')

  reify-natural Î¹       w x = â‰¡-refl
  reify-natural (a â‡’ b) w x = let open â‰¡-Reasoning in begin
    wkNf w (reify-fun (a â‡’ b) x)
      â‰¡âŸ¨âŸ©
    lam (wkNf (keep[ a ] w) (reify-fun b (x .apply freshWk[ _ , a ] _)))
      â‰¡âŸ¨ cong lam (reify-natural b (keep[ a ] w) _) âŸ©
    lam (reify-fun b (wk[ evalTy b ] (keep[ a ] w) (x .apply freshWk[ _ , a ] _)))
      â‰¡âŸ¨ cong lam (reify-pres-â‰‹ b (x .natural freshWk (keep[ a ] w) _)) âŸ©
    lam (reify-fun b (x .apply (freshWk[ _ , a ] âˆ™ keep[ a ] w) (wk[ evalTy a ] (keep[ a ] w) _)))
      â‰¡âŸ¨ cong lam (reify-pres-â‰‹ b (x .apply-â‰‹ _ (reflect-natural a (keep[ a ] w) _)))  âŸ©
    lam (reify-fun b (x .apply (freshWk[ _ , a ] âˆ™ keep[ a ] w) (reflect-fun a (wkNe (keep[ a ] w) _))))
      â‰¡âŸ¨  congâ‚‚ (Î» w n â†’ lam (reify-fun b (x .apply w (reflect-fun a n)))) (cong drop (â‰¡-trans (âŠ†-trans-unit-left _) (â‰¡-sym (âŠ†-trans-unit-right _)))) â‰¡-refl âŸ©
    lam (reify-fun b (x .apply (w âˆ™ freshWk[ _ , a ]) _))
      â‰¡âŸ¨âŸ©
    reify-fun (a â‡’ b) (wk[ evalTy (a â‡’ b) ] w x) âˆ
  reify-natural (â—‡ a)   w x = let open â‰¡-Reasoning in begin
    wk[ Nf'- (â—‡ a) ] w (reify-fun (â—‡ a) x)
      â‰¡âŸ¨âŸ©
    wk[ Nf'- (â—‡ a) ] w (collectNf-fun (â—‡'-map-fun (reify-fun a) x))
      â‰¡âŸ¨ collectNf-natural w (â—‡'-map-fun (reify-fun a) x) âŸ©
    collectNf-fun (wk[ â—‡' Nf'- a ] w (â—‡'-map-fun (reify-fun a) x))
      â‰¡âŸ¨ collectNf-pres-â‰‹ (â—‡'-map-natural (reify-natural a) w x) âŸ©
    collectNf-fun (â—‡'-map-fun (reify-fun a) (wk[ Ty'- (â—‡ a) ] w x))
      â‰¡âŸ¨âŸ©
    reify-fun (â—‡ a) (wk[ Ty'- (â—‡ a) ] w x) âˆ

reflect : (a : Ty) â†’ Ne'- a â†’Ì‡ Ty'- a
reflect a = record
  { fun     = reflect-fun a
  ; pres-â‰‹  = reflect-pres-â‰‹ a
  ; natural = reflect-natural a
  }

reify : (a : Ty) â†’ Ty'- a â†’Ì‡ Nf'- a
reify a = record
  { fun     = reify-fun a
  ; pres-â‰‹  = reify-pres-â‰‹ a
  ; natural = reify-natural a
  }

-- monotonicity lemma
wkTy' : (a : Ty) â†’ (w : Î“ âŠ† Î“') â†’ (x : Ty' Î“ a) â†’ Ty' Î“' a
wkTy' a = wk[ evalTy a ]

-- monotonicity lemma
wkSub' : (Î” : Ctx) â†’ (w : Î“ âŠ† Î“') â†’ (Ï : Sub' Î“ Î”) â†’ Sub' Î“' Î”
wkSub' Î” = wk[ evalCtx Î” ]

-- identity environment
idEnv : (Î“ : Ctx) â†’ Sub' Î“ Î“
idEnv []       = _
idEnv (Î“ `, a) = elem (wkSub' Î“ freshWk (idEnv Î“) , reflect a .apply (var zero))

-- retraction of interpretation
quot : Sub'- Î“ â†’Ì‡ Ty'- a â†’ Nf Î“ a
quot {Î“} {a} f = reify a .apply (f .apply (idEnv Î“))

-- normalization function
norm : Tm Î“ a â†’ Nf Î“ a
norm t = quot (eval t)

-- normalization is complete
norm-complete : (tâ‰ˆu : t â‰ˆ u) â†’ norm t â‰¡ norm u
norm-complete {Î“} {a} tâ‰ˆu = reify-pres-â‰‹ a (apply-sq (evalTm-sound tâ‰ˆu) â‰‹[ evalCtx Î“ ]-refl)

